pipeline {
    agent any
    stages {
        stage('Build info') {
            steps {
              sh 'env'
            }
        }
        stage('Checkout sources') {
            steps {
                checkout changelog: false, poll: false,
                    scm: [$class: 'GitSCM', branches: [[name: "${env.GIT_BRANCH}"]],
                    doGenerateSubmoduleConfigurations: false, extensions: [], submoduleCfg: [],
                    userRemoteConfigs: [[url: "${env.GIT_URL}"]]]
            }
        }
        stage('Set maintenance mode off (old image)') {
            /* Set the maintenance mode to off with the old image;
             * otherwise the upgrade will not start. */
            steps {
                script {
                    openshift.withCluster() {
                        openshift.withProject(/*"${env.PROJECT_NAME}"*/) {
                            def template = readFile 'nextcloud-maintenance.yaml'
                            def config = openshift.process(template,
                              '-p', "NEXTCLOUD_IMAGE_TAG=${env.OLD_NEXTCLOUD_IMAGE_TAG}",
                              '-p', "ON_OFF=off")
                            def jobs = openshift.create(config)
                            timeout(5) {
                                jobs.untilEach(1) {
                                    def object = it.object()
                                    print object.status
                                    print object.status.succeeded
                                    return (object.status.succeeded == 1)
                                }
                            }
                        }
                    }
                }
            }
        }
        stage('Run upgrade with new image') {
            /* First, apply the image stream to create it if it does not exist yet.
             * Second, run the upgrade job with the new image. This will set the nextcloud
             * to maintenance mode. Then, the database and the apps will be upgraded. */
            steps {
                script {
                    openshift.withCluster() {
                        openshift.withProject(/*"${env.PROJECT_NAME}"*/) {
                            def imageTemplate = readFile 'upgrade/nextcloud-image.yaml'
                            def imageConfig = openshift.process(imageTemplate,
                              '-p', "NEXTCLOUD_IMAGE_TAG=${env.NEW_NEXTCLOUD_IMAGE_TAG}")
                            openshift.apply(imageConfig)

                            def upgradeTemplate = readFile 'upgrade/nextcloud-upgrade.yaml'
                            def upgradeConfig = openshift.process(upgradeTemplate,
                              '-p', "NEXTCLOUD_IMAGE_TAG=${env.NEW_NEXTCLOUD_IMAGE_TAG}")
                            def objects = openshift.create(upgradeConfig)
                            timeout(20) {
                                objects.narrow('job').untilEach(1) {
                                    return (it.object().status.succeeded == 1)
                                }
                            }
                        }
                    }
                }
            }
        }
        stage('Build new fixed image (if requested)') {
            /* If the permission fixed is requested, run the build job
             * for the new image. */
            steps {
                script {
                    if (env.BUILD_FIXED_IMAGE.toBoolean()) {
                        openshift.withCluster() {
                            openshift.withProject(/*"${env.PROJECT_NAME}"*/) {
                                def imageBuildTemplate = readFile 'nc_image_fix/nextcloud-image-fix.yaml'
                                def imageBuildConfig = openshift.process(imageBuildTemplate,
                                    '-p', "NEXTCLOUD_IMAGE_TAG=${env.NEW_NEXTCLOUD_IMAGE_TAG}")
                                openshift.apply(imageBuildConfig)
                                def buildSelector = openshift.selector("bc", 'nextcloud-image')
                                buildSelector.startBuild("--follow=true")
                                /* Alternatively to "--follow=true":
                                 * Do some parallel tasks while building.
                                 * When needed to wait for the build again and show logs, do:
                                 * build.logs('-f') */
                            }
                        }
                    }
                }
            }
        }
        stage('Apply new image to deployment configuration') {
            steps {
                script {
                    openshift.withCluster() {
                        openshift.withProject(/*"${env.PROJECT_NAME}"*/) {
                            def template = readFile 'nextcloud.yaml'
                            def config = null
                            if (env.BUILD_FIXED_IMAGE.toBoolean()) {
                                config = openshift.process(template,
                                  '-p', "NEXTCLOUD_HOST=${env.NEXTCLOUD_HOST}",
                                  '-p', "NEXTCLOUD_IMAGESTREAM_NAME=nextcloud-fixed",
                                  '-p', "NEXTCLOUD_IMAGE_TAG=latest")
                            } else {
                                config = openshift.process(template,
                                  '-p', "NEXTCLOUD_HOST=${env.NEXTCLOUD_HOST}",
                                  '-p', "NEXTCLOUD_IMAGE_TAG=${env.NEW_NEXTCLOUD_IMAGE_TAG}")
                            }
                            openshift.apply(config)

                            def cronTemplate = readFile 'nextcloud-cron.yaml'
                            // The cron job will always be done with the unfixed image
                            def cronConfig = openshift.process(cronTemplate,
                                  '-p', "NEXTCLOUD_IMAGE_TAG=${env.NEW_NEXTCLOUD_IMAGE_TAG}")
                            openshift.apply(cronConfig)

                            def templateName = 'nextcloud'
                            /*def rm = openshift.selector("dc", templateName)
                              .rollout().latest()*/
                            timeout(5) {
                                openshift.selector("dc", templateName)
                                  .related('pods').untilEach(1) {
                                    return (it.object().status.phase == "Running")
                                }
                            }
                        }
                    }
                }
            }
        }
        stage('Set maintenance mode off (new image)') {
            steps {
                script {
                    openshift.withCluster() {
                        openshift.withProject(/*"${env.PROJECT_NAME}"*/) {
                            def template = readFile 'nextcloud-maintenance.yaml'
                            def config = openshift.process(template,
                              '-p', "NEXTCLOUD_IMAGE_TAG=${env.NEW_NEXTCLOUD_IMAGE_TAG}",
                              '-p', "ON_OFF=off")
                            def jobs = openshift.create(config)
                            timeout(10) {
                                jobs.untilEach(1) {
                                    return (it.object().status.succeeded == 1)
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}
